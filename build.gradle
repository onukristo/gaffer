buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}
	dependencies {
		classpath 'net.researchgate:gradle-release:2.4.0'
	}
}

apply plugin: 'idea'

idea {
	project {
		vcs = 'Git'
		jdkName = '1.8'
		languageLevel = '1.8'
	}
}

apply plugin: 'java'
apply plugin: 'findbugs'

group = 'ee.homies.gaffer'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

jar {
	manifest {
		attributes(
				"Implementation-Title": "Gaffer",
				"Implementation-Version": version
		)
	}
}

compileJava {
	options.encoding = 'utf-8'
}

repositories {
	mavenLocal()
	mavenCentral()
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
}

dependencies {
	compile 'javax.transaction:javax.transaction-api:1.2'
	compile 'javax.jms:jms-api:1.1-rev-1'
	compile 'org.slf4j:slf4j-api:1.7.13'
	compile 'javax.annotation:jsr250-api:1.0'
	compile 'org.apache.commons:commons-lang3:3.4'
	compile 'com.google.code.findbugs:annotations:3.0.1u2'

	testCompile 'org.springframework:spring-tx:4.2.3.RELEASE'
	testCompile 'org.springframework:spring-test:4.2.3.RELEASE'
	testCompile 'org.springframework:spring-jdbc:4.2.3.RELEASE'
	testCompile 'org.springframework:spring-context:4.2.3.RELEASE'
	testCompile 'org.springframework:spring-jms:4.2.3.RELEASE'
	testCompile 'org.springframework:spring-orm:4.2.3.RELEASE'
	testCompile 'org.hibernate:hibernate-core:4.3.11.Final'
	testCompile 'com.google.guava:guava:18.0'
	testCompile 'junit:junit:4.12'
	testCompile 'org.hamcrest:hamcrest-core:1.3'
	testCompile 'org.hsqldb:hsqldb:2.3.0'
	testCompile 'org.apache.tomcat:tomcat-jdbc:8.0.28'
	testCompile 'org.apache.activemq:activemq-pool:5.7.0'
	testCompile 'ch.qos.logback:logback-classic:1.0.13'
	testCompile 'org.apache.xbean:xbean-spring:3.14'
}

apply plugin: 'net.researchgate.release'

release {
	tagTemplate = '$name-$version'
}

apply plugin: 'maven-publish'
afterReleaseBuild.dependsOn publish

task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId jar.baseName
			from components.java

			artifact sourceJar {
				classifier "sources"
			}
		}
	}
	repositories {
		maven {
			url "http://artifacts.transferwise.com/artifactory/libs-${project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release'}-local"
			credentials {
				username = artifactoryUser
				password = artifactoryPassword
			}
		}
	}
}

